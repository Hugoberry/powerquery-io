---
title: BinaryFormat.Group
---

# BinaryFormat.Group


## Description

Возвращает двоичный формат, который считывает группу элементов.


## Syntax

```powerquery
BinaryFormat.Group(
    binaryFormat as function,
    group as list,
    optional extra as function,
    optional lastKey as any
) as function
```


## Details

Используются следующие параметры.<ul><li>Параметр <code>binaryFormat</code> указывает двоичный формат значения ключа.</li><li>Параметр <code>group</code> предоставляет сведения о группе известных элементов.</li><li>Необязательный параметр <code>extra</code> можно использовать для определения функции, возвращающей значение двоичного формата для значения, идущего после любого непредвиденного ключа.  Если параметр <code>extra</code> не указан, при наличии непредвиденных значений ключей возникнет ошибка.</li></ul>Параметр <code>group</code> указывает список определений элементов.  Каждое определение элемента - это список, содержащий 3-5 значений:<ul><li>Значение ключа. Значение ключа, который соответствует элементу.  Это должно быть уникальное значение в наборе элементов.</li><li>Формат элемента. Двоичный формат, соответствующий значению элемента. Это позволяет каждому элементу использовать свой формат. </li><li>Вхождение элемента. Значение <code>BinaryOccurrence.Type</code>, которое определяет, сколько раз элемент будет указан в группе. Если обязательные элементы отсутствуют, возникает ошибка. Обязательные или необязательные повторяющиеся элементы обрабатываются как непредвиденные значения ключа.</li><li>Значение элемента по умолчанию (необязательно). Если значение элемента по умолчанию отображается в списке определений элемента и не равно NULL, оно будет использоваться вместо значения по умолчанию. Значение по умолчанию для повторяющихся и необязательных элементов равно NULL, а значение по умолчанию для повторяющихся значений — пустой список \{ }.</li><li>Преобразование значения элемента (необязательно). Если функция преобразования значения элемента присутствует в списке определений элемента и не равна NULL, она будет вызвана для преобразования значения элемента перед его возвращением. Функция преобразования вызывается, только если элемент присутствует в входных данных (она не вызывается со значением по умолчанию). </li></ul>


## Examples

### Example #1 
Далее предполагается, что значение ключа - это один байт, в группе 4 элемента, во всех из которых после ключа идет байт данных.  Элементы отображаются во входных данных следующим образом:&lt;ul&gt;&lt;li&gt;Ключ 1 является обязательным и не отображается со значением 11.&lt;/li&gt;&lt;li&gt;Ключ 2 повторяется и отображается дважды со значением 22 и приводит к формированию значения \{ 22, 22 }.&lt;/li&gt;&lt;li&gt;Ключ 3 не является обязательным, не отображается и приводит к формированию значения NULL.&lt;/li&gt;&lt;li&gt;Ключ 4 повторяется, но не отображается и приводит к формированию значения \{ }.&lt;/li&gt;&lt;li&gt;Ключ 5 не является частью группы, но появляется один раз со значением 55.  Дополнительная функция вызывается со значением ключа 5 и возвращает формат, соответствующий этому значению (BinaryFormat.Byte).  Значение 55 считывается и удаляется.&lt;/li&gt;&lt;/ul&gt;
```powerquery
let
    b = #binary({
        1, 11,
        2, 22,
        2, 22,
        5, 55,
        1, 11
    }),
    f = BinaryFormat.Group(
        BinaryFormat.Byte,
        {
            {1, BinaryFormat.Byte, BinaryOccurrence.Required},
            {2, BinaryFormat.Byte, BinaryOccurrence.Repeating},
            {3, BinaryFormat.Byte, BinaryOccurrence.Optional},
            {4, BinaryFormat.Byte, BinaryOccurrence.Repeating}
        },
        (extra) => BinaryFormat.Byte
    )
in
    f(b)
```

Result: 
```powerquery
{11, {22, 22}, null, {}}
```


### Example #2 
В следующем примере показано преобразование значения элемента и значения элемента по умолчанию.   Повторяющийся элемент с ключом 1 суммирует список значений, считанных с помощью List.Sum.  Необязательный элемент с ключом 2 имеет значение по умолчанию 123, а не NULL.
```powerquery
let
    b = #binary({
        1, 101,
        1, 102
    }),
    f = BinaryFormat.Group(
        BinaryFormat.Byte,
        {
            {1, BinaryFormat.Byte, BinaryOccurrence.Repeating,
              0, (list) => List.Sum(list)},
            {2, BinaryFormat.Byte, BinaryOccurrence.Optional, 123}
        }
    )
in
    f(b)
```

Result: 
```powerquery
{203, 123}
```




## Category
Binary Formats.Reading a group of items
